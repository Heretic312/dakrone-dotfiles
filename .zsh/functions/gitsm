### vim:ft=zsh:foldmethod=marker
###
### git send-email + template generation
###
# This works for a patch series that is at the top of a branch;
# E.g. created by: git format-patch -5
# ...for a series of the last 5 commits on the current branch.
# Or for sending out a series that corresponds to a topic-branch:
#   % rm -f 0*.patch
#   % git format-patch master..topicbranch
#   % gitsm 0*.patch
# Creates a List of commit-lines and a diffstat for all commits.
local gsmoptions tempfile GIT_EDITOR

gsmoptions=()

if [[ $1 == '--' ]] ; then
    shift
    while [[ $# -gt 0 ]] ; do
        if [[ $1 == '--' ]] ; then
            shift
            break
        fi
        gsmoptions+=($1)
        shift
    done
fi

if [[ -z $1 ]] ; then
    printf 'gitsm [-- <options for git send-email> --] <format-patches>...\n'
    return 1
fi

tempfile=$(mktemp ${TMPDIR:-/tmp}/gitsm.XXXXXXXXXX)
if [[ $? -gt 0 ]] ; then
    printf 'tempfile creation failed.\n'
    return 1
fi

[[ -e ${tempfile} ]] && rm ${tempfile}
git shortlog -${#@} | sed -e 's,^   *,  ,' >> ${tempfile}
printf '\n' >> ${tempfile}
git diff --stat 'HEAD~'${#@} >> ${tempfile}
GIT_EDITOR='vim +":normal Go" \
                +":r'${tempfile}'" \
                +":normal 2G$"' \
    git send-email ${gsmoptions} --compose --subject "[PATCH 0/${#@}] " "${@}"
rm ${tempfile}
