#zdep zshrc.d/options,zshrc.d/modules
### vim:ft=zsh:foldmethod=marker
###
### styles - zstyle invocation for everything except compsys
###
### Frank Terbeck <ft@bewatermyfriend.org>
### Updated by:
### Lee Hinman <lee at writequit dot org>
###
### URI: <http://ft.bewatermyfriend.org/comp/zsh.html>
###

##
#   vcs_info setup
#
zstyle ':vcs_info:*' actionformats " $Pfg[ma]($PNC%s$Pfg[ma])$Pfg[ye]-$Pfg[ma][$Pfg[grn]%b$Pfg[ye]|$Pfg[red]%a$Pfg[ma]]$PNC-" \
                                   "zsh: %r"
zstyle ':vcs_info:*' formats       " $Pfg[ma]($PNC%s$Pfg[ma])$Pfg[ye]-$Pfg[ma][$Pfg[grn]%b$Pfg[ma]]$PNC%}-" \
                                   "zsh: %r"

zstyle ':vcs_info:git:*' actionformats " $Pfg[ma][$Pfg[grn]%b$Pfg[red]%c%u$Pfg[ye]|$Pfg[red]%a$Pfg[ma]]$PNC-" \
                                       "zsh: %r"
zstyle ':vcs_info:git:*' formats       " $Pfg[ma][$Pfg[grn]%b$Pfg[red]%c%u$Pfg[ma]]$PNC-" \
                                       "zsh: %r"
zstyle ':vcs_info:cvs:*' formats       " $Pfg[ma]($PNC%s$Pfg[ma])$Pfg[ye]-$Pfg[ma][$Pfg[grn]%r$Pfg[ma]]$PNC-" \
                                       "zsh: %r"

zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat "%b$Pfg[red]:$Pfg[ye]%r"

zstyle ':vcs_info:bzr:*' use-simple true
zstyle ':vcs_info:*'     enable git hg cvs svn

zstyle ':vcs_info:git:*' check-for-changes false

##
#   acceptline setup
#
zstyle ':acceptline:empty' call_default false

if zis_431 "atleast" ; then
    ##
    #   lookup setup
    #
    zstyle ':lookup:*'         use-pager   true
    zstyle ':lookup:*'         pager-auto  true
    zstyle ':lookup:*'         pager       'less -M'
    zstyle ':lookup:*'         txt-browser 'w3m'
    zstyle ':lookup:*'         txt-formats '%s'
    zstyle ':lookup:*'         gui-browser 'firefox'
    zstyle ':lookup:*'         gui-formats '-remote' 'openurl(%s)'
    zstyle ':lookup:*:rfc:*'   search-path ~/share/rfc /usr/share/doc/RFC/links
    zstyle ':lookup:*:rfc:*'   save-path   ~/share/rfc

    # Here's a nice one: I got this script cmus-rstat.sh, which tells me which
    # track cmus (my audio player) is currently playing in this format:
    #       np: Artist - Album - Track
    #
    # Well, that's easily parsable. :)
    # Now I can make automated searches via letssingit.com with lookup's
    # query-handler feature like this:
    #
    # If I want to search for the artist:
    #   % lookup -Q letssingit -m artist
    # or the album:
    #   % lookup -Q letssingit -m album
    # or the track name (for instant sing-along :->):
    #   % lookup -Q letssingit -m song
    #
    zstyle ':lookup:*:letssingit:*' query-handlers letssingit
    function LOOKUP_qh_letssingit() {
        local mode=${lookup_communicate[mode]}
        local np

        case ${mode} in
        (artist|album|song)
            np="$(cmus-rstat.sh)"
            np=${np/np: /}
            if [[ ${np} == '[cmus]: -stopped-' ]] ; then
                printf '\n  Cmus is currently playing nothing!\n\n'
                return 3
            fi
            ;;
        esac

        case ${mode} in
        (artist)
            QUERY=${np%% - *}
            ;;
        (album)
            np=${np% - *}
            if [[ ${np} == *' - '* ]] ; then
                QUERY=${np#* - }
            else
                printf '\n Could not get album (%s)!\n\n' "$(cmus-rstat.sh)"
                return 3
            fi
            ;;
        (song)
            QUERY=${np##* - }
            ;;
        esac

        return 0
    }

    # aliases for lookup backends
    #lookup -a s=google
    #lookup -a d=leo
fi

##
#   exec_on_xclip setup
#
#zstyle ':exec_on_xclip:x11:*' operate false
zstyle ':exec_on_xclip:*' contexts "0:uri_http"  'http://*'  \
                                   "1:file_gz"   '(/|~)*.gz' \
                                   "2:file"      '(/|~)*'
zstyle ':exec_on_xclip:*' stages   '0:nox' '1:x11'
zstyle ':exec_on_xclip:*' xclip    "xclip"
zstyle ':exec_on_xclip:*' options  "-o"
zstyle ':exec_on_xclip:*' pager    "less -Mr"
zstyle ':exec_on_xclip:nox:*'         clearterm true
if   [[ -x $(which opera) ]] ; then
    zstyle ':exec_on_xclip:x11:uri_http'  app 'opera -remote openURL(_eoxc_)'
elif [[ -x $(which firefox) ]] ; then
    zstyle ':exec_on_xclip:x11:uri_http'  app 'firefox -new-tab _eoxc_'
else
    zstyle ':exec_on_xclip:x11:uri_http'  app false
fi
zstyle ':exec_on_xclip:*:uri_http'    app "${BROWSER:-w3m} _eoxc_"
zstyle ':exec_on_xclip:*:file_gz'     app "zless _eoxc_"
zstyle ':exec_on_xclip:*:file'        app "less _eoxc_"
